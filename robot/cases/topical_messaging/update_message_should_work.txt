*** Settings ***
Resource          ../../resources/s3-fakeclient/topical_messaging.txt
Resource          ../../resources/data/topical_messaging_data.txt
Test Setup        Run Keywords  On Node 0  AND  With Test Data  ${DB}  AND  Start Synthese On Node 0
Test Teardown     Stop Synthese On Node 0


*** Keyword ***

A Scenario Template "${scenario_template_name}" Containing One Message With Title "${message_title}" And Content "${message_content}" Is Instantiated As "${scenario_name}"
    ${scenario_template_id}=  Scenario Template Is Created With Name "${scenario_template_name}" Containing One Message With Title "${message_title}" And Content "${message_content}"
    ${scenario_id}=  Scenario "${scenario_name}" Is Instanciated From ${scenario_template_id}
    [Return]   ${scenario_id}


*** Test Cases ***

Instanciating A Scenario Template Should Work
    ${scenario_id}=  A Scenario Template "${SCENARIO_TEMPLATE_NAME}" Containing One Message With Title "${MESSAGE_TITLE1}" And Content "${MESSAGE_CONTENT1}" Is Instantiated As "${SCENARIO_NAME1}"
    ${scenario_xml}=   Scenario ${scenario_id} Is Requested
    Name Of Scenario ${scenario_xml} Is ${SCENARIO_NAME1}
    Title Of The Message Of Scenario ${scenario_xml} Is ${MESSAGE_TITLE1}
    Content Of The Message Of Scenario ${scenario_xml} Is ${MESSAGE_CONTENT1}
    Name Of The Template Of Scenario ${scenario_xml} Is ${SCENARIO_TEMPLATE_NAME}
    Title Of The Message Of The Template Of Scenario ${scenario_xml} Is ${MESSAGE_TITLE1}
    Content Of The Message Of The Template Of Scenario ${scenario_xml} Is ${MESSAGE_CONTENT1}


Updating A Message From An Instanciated Scenario Template Should Work
    ${scenario_id}=   A Scenario Template "${SCENARIO_TEMPLATE_NAME}" Containing One Message With Title "${MESSAGE_TITLE1}" And Content "${MESSAGE_CONTENT1}" Is Instantiated As "${SCENARIO_NAME1}"
	Single Message Of Scenario ${scenario_id} Is Updated With Title "${MESSAGE_TITLE2}" And Content "${MESSAGE_CONTENT2}"
    ${scenario_xml}=   Scenario ${scenario_id} Is Requested
    Name Of Scenario ${scenario_xml} Is ${SCENARIO_NAME1}
    Title Of The Message Of Scenario ${scenario_xml} Is ${MESSAGE_TITLE2}
    Content Of The Message Of Scenario ${scenario_xml} Is ${MESSAGE_CONTENT2}
    Name Of The Template Of Scenario ${scenario_xml} Is ${SCENARIO_TEMPLATE_NAME}
    Title Of The Message Of The Template Of Scenario ${scenario_xml} Is ${MESSAGE_TITLE1}
    Content Of The Message Of The Template Of Scenario ${scenario_xml} Is ${MESSAGE_CONTENT1}

