*** Settings ***
Resource          ../../resources/s3-fakeclient/import_services.txt    
Resource          ../../resources/s3-fakeclient/object_services.txt    
Test Setup        Start Synthese On Node 0
Test Teardown     Stop Synthese On Node 0

*** Test Cases ***

Import PubliBike Stations Of Lausanne-Morges Should Work
    On node 0
    ${publibike_stations_file_path}=  Set Variable   ${CURDIR}/../../resources/data/2015_publibike_lausanne_morges.csv
    Create City  name=LAUSANNE
    Create City  name=PAUDEX
    Create City  name=RENENS
    Create City  name=MORGES
    Create City  name=PREVERENGES
    Create City  name=ECUBLENS
    ${expected_public_bike_network_count}=   Set Variable  1
    ${expected_public_bike_station_count}=   Set Variable  26
    Import PubliBike Stations  ${publibike_stations_file_path}
    There Are ${expected_public_bike_network_count} Objects In Table ${PUBLIC_BIKE_NETWORKS_TABLE_ID}
    There Are ${expected_public_bike_station_count} Objects In Table ${PUBLIC_BIKE_STATIONS_TABLE_ID}
    ${public_bike_network_id}=  Get Attribute id Of Object 1 In Table ${PUBLIC_BIKE_NETWORKS_TABLE_ID}
    Every Object In Table ${PUBLIC_BIKE_STATIONS_TABLE_ID} Should Have Attribute public_bike_network_id Equal To "${public_bike_network_id}"

*** Keywords ***

Create City
    [Arguments]    ${name}    ${code}=
    ${http_response}=  Create Synthese Object    ${CITIES_TABLE_ID}    field_name\=${name}    field_code\=${code}
