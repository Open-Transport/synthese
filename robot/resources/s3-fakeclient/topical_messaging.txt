*** Settings ***
Resource          ../s3-server/topical_messaging.txt
Library           String
Library           XML

*** Variables ***

*** Keywords ***

Topical Message ${message_id} Is Created With Title "${message_title}" And Content "${message_content}"
    Set Test Variable   ${message.id}   ${message_id}
    Set Test Variable   ${message.title}   ${message_title}
    Set Test Variable   ${message.content}   ${message_content}

    
Topical Message ${message_id} Is Bounded To Broadcast Point Type ${broadcast_point_type_id}
    Set Test Variable   ${broadcast_point_type_id}   ${broadcast_point_type_id}

        
Broadcast Of ${message_id} Is Enabled
    ${http_response}=  Call Synthese Action   scenario_save   actionParamt\=0   actionParam_created_message_title\=${message.title}   actionParamme\=${message.content}   actionParamsid\=${message.id}   actionParamis\=16607027920896004   actionParamena\=1   actionParam_recipients_displayscreen\=${broadcast_point_type_id}   actionParamdv\=%20  actionParamnam\=00     actionParamle=10
    Should Be Equal As Strings    ${http_response.status_code}    200



A Topical Message Is Broadcasted On Broadcast Point ${broadcast_point_id} 
    ${response}=   List Display Screen Messages     ${broadcast_point_id} 
    ${utf8_content}   Decode Bytes To String   ${response.content}   UTF-8    strict
    ${messages_root}=   Parse XML   ${utf8_content}
    Set Test Variable   ${messages}   ${messages_root}
    Element Should Exist	${messages}	  xpath=message


Content Of This Topical Message Is "${message_content}"
    Element Attribute Should Be	 ${messages}  content  ${message_content}  xpath=message[1]


Title Of This Topical Message Is "${message_title}"
    Element Attribute Should Be  ${messages}  title  ${message_title}  xpath=message[1]


Scenario ${scenario_id:\d+} Is Requested
    ${response}=   Call Synthese Service   scenario   roid\=${scenario_id}   of\=xml
    ${utf8_content}   Decode Bytes To String   ${response.content}   UTF-8    strict
    ${scenario_root}=   Parse XML   ${utf8_content}
    Set Test Variable   ${scenario.xml}   ${scenario_root}
    Log   ${utf8_content}
    Element Should Exist	${scenario_root}	  xpath=calendar/message


Name Of This Scenario Is "${scenario_name}"
    Element Attribute Should Be	 ${scenario.xml}  name  ${scenario_name}


Title Of The Topical Message Of This Scenario Is "${message_title}"
    Element Attribute Should Be  ${scenario.xml}  title  ${message_title}  xpath=calendar[1].message[1]


Content Of The Topical Message Of This Scenario Is "${message_content}"
    Element Attribute Should Be  ${scenario.xml}  content  ${message_content}  xpath=calendar[1].message[1]


Name Of This Scenario Template Is "${scenario_name}"
    Element Attribute Should Be	 ${scenario.xml}  name  ${scenario_name}  xpath=template_scenario[1]


Title Of The Topical Message Of This Scenario Template Is "${message_title}"
    Element Attribute Should Be  ${scenario.xml}  title  ${message_title}  xpath=template_scenario[1].calendar[1].message[1]


Content Of The Topical Message Of This Scenario Template Is "${message_content}"
    Element Attribute Should Be  ${scenario.xml}  content  ${message_content}  xpath=template_scenario[1].calendar[1].message[1]


Scenario Template "${scenario_name}" Is Created Containing One Message With Title "${message_title}" And Content "${message_content}"
    ${response}=   Call Synthese Action   scenario_save   actionParamt\=1   actionParamnam\=${scenario_name}   actionParam_json\={"calendar":[{"id":"","name":"","period":[],"message":[{"id":"","calendar_rank":"1","title":"${message_title}","content":"${message_content}","level":"10","displayDuration":"","digitized_version":"","alternative":[],"section":[{"id":""}],"line_recipient":[],"stoparea_recipient":[],"displayscreen_recipient":[],"rank":0}],"rank":1}]}
    Should Be Equal As Strings    ${response.status_code}    200
    ${utf8_content}=   Decode Bytes To String   ${response.content}   UTF-8    strict
    ${xml_response}=   Parse XML   ${utf8_content}
	${scenario_id}=   Get Element Attribute   source=${xml_response}   name=generated_id
    [Return]    ${scenario_id}


Scenario "${scenario_name}" Is Instanciated From ${scenario_template_id}
    ${response}=   Call Synthese Action   scenario_save   actionParamt\=0   actionParamtpl\=${scenario_template_id}   actionParamnam\=${scenario_name}
    Should Be Equal As Strings    ${response.status_code}    200
    ${utf8_content}=   Decode Bytes To String   ${response.content}   UTF-8    strict
    ${xml_response}=   Parse XML   ${utf8_content}
	${scenario_id}=   Get Element Attribute   source=${xml_response}   name=generated_id
    [Return]    ${scenario_id}


Single Message Of Scenario ${scenario_id} Is Updated With Title "${message_title}" And Content "${message_content}"
    Scenario ${scenario_id} Is Requested
    ${scenario.xml}=    Set Element Attribute   ${scenario.xml}   title     ${message_title}     calendar[1]/message[1]
    ${scenario.xml}=    Set Element Attribute   ${scenario.xml}   content   ${message_content}   calendar[1]/message[1]
    ${scenario_json}=   Convert Scenario From XML To Json ${scenario.xml}
    ${response}=   Call Synthese Action   scenario_save   actionParamsid\=${scenario_id}   actionParam_json\=${scenario_json}
    Should Be Equal As Strings    ${response.status_code}    200


This Scenario Is Named "${scenario_name}" 
    Element Attribute Should Be	 ${scenario.xml}  name  ${scenario_name}


This Scenario Contains One Message With Title "${message_title}" And Content "${message_content}"
    Element Attribute Should Be  ${scenario.xml}  title    ${message_title}    xpath=calendar[1]/message[1]
    Element Attribute Should Be  ${scenario.xml}  content  ${message_content}  xpath=calendar[1]/message[1]


Convert Scenario From XML To Json ${scenario_xml}
    ${calendar_id}=      Get Element Attribute   ${scenario_xml}   id           calendar[1]
    ${calendar_name}=    Get Element Attribute   ${scenario_xml}   name         calendar[1]
    ${message_id}=       Get Element Attribute   ${scenario_xml}   message_id   calendar[1]/message[1]
    ${message_title}=    Get Element Attribute   ${scenario_xml}   title        calendar[1]/message[1]
    ${message_content}=  Get Element Attribute   ${scenario_xml}   content      calendar[1]/message[1]
    ${scenario_json}=    Set Variable   {\"calendar\":[{\"id\":\"${calendar_id}\",\"name\":\"${calendar_name}\",\"period\":[],\"message\":[{\"id\":\"${message_id}\",\"calendar_rank\":\"\",\"title\":\"${message_title}\",\"content\":\"${message_content}\",\"level\":\"10\",\"displayDuration\":\"\",\"digitized_version\":\"\",\"alternative\":[],\"section\":[{\"id\":\"\"}],\"line_recipient\":[],\"stoparea_recipient\":[],\"displayscreen_recipient\":[],\"rank\":0}],\"rank\":1}]}
    Log   ${scenario_json}
    [Return]  ${scenario_json}

