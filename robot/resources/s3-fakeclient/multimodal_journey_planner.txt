*** Settings ***
Resource          ../s3-server/multimodal_journey_planner.txt


*** Variables ***


*** Keywords ***

Search Walk Journey From "${departure_city}, ${departure_place}" To "${arrival_city}, ${arrival_place}" On ${departure_date} At ${departure_time}
    ${response}=  MultimodalJourneyPlanner  departure_city=${departure_city}  departure_place=${departure_place}  arrival_city=${arrival_city}  arrival_place=${arrival_place}  departure_date=${departure_date}  departure_time=${departure_time}  use_walk=1  use_pt=0
    Log Many  ${response.content}
    Should Be Equal As Strings  ${response.status_code}  200
    ${utf8_content}=  Decode Bytes To String  ${response.content}  UTF-8  strict
    ${xml_response}=  Parse XML  ${utf8_content}
    Set Test Variable  ${xml_solutions}  ${xml_response}

Search Walk And Public Transport Journey From "${departure_city}, ${departure_place}" To "${arrival_city}, ${arrival_place}" On ${departure_date} At ${departure_time}
    ${response}=  MultimodalJourneyPlanner  departure_city=${departure_city}  departure_place=${departure_place}  arrival_city=${arrival_city}  arrival_place=${arrival_place}  departure_date=${departure_date}  departure_time=${departure_time}  use_walk=1  use_pt=1
    Log Many  ${response.content}
    Should Be Equal As Strings  ${response.status_code}    200
    ${utf8_content}=   Decode Bytes To String   ${response.content}   UTF-8    strict
    ${xml_response}=   Parse XML   ${utf8_content}
    Set Test Variable  ${xml_solutions}  ${xml_response}

There Should Be ${expected_solutions_count:\d+} Solution(s)
    ${solutions_count}=  Evaluate Xpath  ${xml_solutions}  count(journey)
    Should Be Equal As Numbers  ${expected_solutions_count}  ${solutions_count}

There Should Be ${expected_solutions_count:\d+} Solution(s) Using Only Walk
    ${solutions_count}=  Evaluate Xpath  ${xml_solutions}  count(journey[(count(leg/walk_attributes) > 0) and (count(leg/pt_attributes) = 0)])
    Should Be Equal As Numbers  ${expected_solutions_count}  ${solutions_count}

There Should Be ${expected_solutions_count:\d+} Solution(s) Using Only Public Transport
    ${solutions_count}=  Evaluate Xpath  ${xml_solutions}  count(journey[(count(leg/walk_attributes) = 0) and (count(leg/pt_attributes) > 0)])
    Should Be Equal As Numbers  ${expected_solutions_count}  ${solutions_count}

Solution ${solution_index} Should Start From "${departure_city}, ${departure_place}" And Arrive To "${arrival_city}, ${arrival_place}"
    Element Attribute Should Be  ${xml_solutions}  name  ${departure_city} ${departure_place}  xpath=journey[${solution_index}]/departure
    Element Attribute Should Be  ${xml_solutions}  name  ${arrival_city} ${arrival_place}  xpath=journey[${solution_index}]/arrival

All Solutions Should Start From "${departure_city}, ${departure_place}" And Arrive To "${arrival_city}, ${arrival_place}"
    ${solutions_count}=  Evaluate Xpath  ${xml_solutions}  count(journey)
    :FOR  ${i}  IN RANGE  1  ${solutions_count + 1}
    \  Solution ${i} Should Start From "${departure_city}, ${departure_place}" And Arrive To "${arrival_city}, ${arrival_place}"


