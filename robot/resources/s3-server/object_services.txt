*** Settings ***
Resource          common.txt


*** Variables ***


*** Keywords ***

List Synthese Objects
    [Arguments]  ${table_id}
    ${http_response}=    Call Synthese Service    objectlist    table_id\=${table_id}  of\=xml
    Log Many  ${http_response.content}
    Should Be Equal As Strings    ${http_response.status_code}    200
    ${utf8_content}=   Decode Bytes To String   ${http_response.content}   UTF-8    strict
    [Return]    ${utf8_content}

View Synthese Object
    [Arguments]    ${object_id}
    ${http_response}=    Call Synthese Service    object    roid\=${object_id}  of\=xml
    Log Many  ${http_response.content}
    Should Be Equal As Strings    ${http_response.status_code}    200
    ${utf8_content}=   Decode Bytes To String   ${http_response.content}   UTF-8    strict
    [Return]    ${utf8_content}

Check Synthese Object Exists
    [Arguments]    ${object_id}
    ${http_response}=    Call Synthese Service    object    roid\=${object_id}  of\=xml
    Run Keyword Unless  ${http_response.status_code} == 200  Fail

Create Synthese Object
    [Arguments]    ${table_id}    @{varargs}
    ${http_response}=    Call Synthese Service    objectcreate    table_id\=${table_id}  of\=xml  @{varargs}
    Log Many  ${http_response.content}
    Should Be Equal As Strings    ${http_response.status_code}    200
    ${utf8_content}=   Decode Bytes To String   ${http_response.content}   UTF-8    strict
    [Return]    ${utf8_content}

Delete Synthese Object
    [Arguments]    ${object_id}
    ${http_response}=    Call Synthese Action    RemoveObjectAction    actionParam_object_id\=${object_id}
    Should Be Equal As Strings    ${http_response.status_code}    200

Update Synthese Object
    [Arguments]    ${object_id}   @{varargs}
    ${http_response}=    Call Synthese Action    ObjectUpdate    actionParam_object_id\=${object_id}    @{varargs}
    Should Be Equal As Strings    ${http_response.status_code}    200

Check Synthese Object Field Equals
    [Arguments]    ${object_id}    ${field_name}    ${value}
    ${object_xml}=  View Synthese Object    ${object_id}
    ${field_value}=  Get Element Attribute    source=${object_xml}    name=${field_name}
    Should Be Equal As Strings    ${field_value}    ${value}

